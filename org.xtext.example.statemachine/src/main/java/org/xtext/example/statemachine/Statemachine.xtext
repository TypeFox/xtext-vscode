/*******************************************************************************
 * Copyright (c) 2016 TypeFox GmbH (http://www.typefox.io) and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *******************************************************************************/
grammar org.xtext.example.statemachine.Statemachine with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
generate statemachine "http://www.xtext.org/example/statemachine/Statemachine"

Statemachine :
	{Statemachine}
	signals+=Signal*
	states+=State*;

Signal:
	(InputSignal | OutputSignal) 'signal' name=ID;

InputSignal:
	{InputSignal}
	'input';

OutputSignal:
	{OutputSignal}
	'output';

State:
	'state' name=ID
		commands+=Command*
		transitions+=Transition*
	'end';

Transition:
	'if' condition=Condition 'goto' state=[State];

Condition:
	events+=Event ('and' events+=Event)*;

Event:
	signal=[Signal] '==' value=BOOLEAN;

Command:
	'set' signal=[Signal] '=' newValue=BOOLEAN;

terminal BOOLEAN returns ecore::EBoolean:
	'true' | 'false';
